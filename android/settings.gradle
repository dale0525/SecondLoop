pluginManagement {
    def useChinaMirrors = System.getenv("SECONDLOOP_USE_CN_MIRRORS") == "1"
    def forceTls12 = System.getenv("SECONDLOOP_FORCE_TLS12") == "1"

    if (forceTls12) {
        System.setProperty("https.protocols", "TLSv1.2")
        System.setProperty("jdk.tls.client.protocols", "TLSv1.2")
    }

    def flutterSdkPath = {
        def properties = new Properties()
        file("local.properties").withInputStream { properties.load(it) }
        def flutterSdkPath = properties.getProperty("flutter.sdk")
        assert flutterSdkPath != null, "flutter.sdk not set in local.properties"
        return flutterSdkPath
    }
    settings.ext.flutterSdkPath = flutterSdkPath()

    includeBuild("${settings.ext.flutterSdkPath}/packages/flutter_tools/gradle")

    repositories {
        if (useChinaMirrors) {
            maven { url "https://maven.aliyun.com/repository/google" }
            maven { url "https://maven.aliyun.com/repository/public" }
            maven { url "https://maven.aliyun.com/repository/gradle-plugin" }
        }
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id "dev.flutter.flutter-plugin-loader" version "1.0.0"
    id "com.android.application" version "8.1.0" apply false
    id "org.jetbrains.kotlin.android" version "1.9.10" apply false
}

include ":app"
