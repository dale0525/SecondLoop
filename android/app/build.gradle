plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('key.properties')
def hasReleaseKeystore = false
if (keystorePropertiesFile.exists()) {
    keystorePropertiesFile.withReader('UTF-8') { reader ->
        keystoreProperties.load(reader)
    }
    def storeFilePath = keystoreProperties.getProperty('storeFile')
    if (storeFilePath != null) {
        hasReleaseKeystore = rootProject.file(storeFilePath).exists()
    }
}

def isReleaseBuildRequested = gradle.startParameter.taskNames.any { taskName ->
    taskName.toLowerCase().contains("release")
}

android {
    namespace "com.secondloop.secondloop"
    compileSdkVersion 35
    ndkVersion flutter.ndkVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "com.secondloop.secondloop"
        // You can update the following values to match your application needs.
        // For more information, see: https://docs.flutter.dev/deployment/android#reviewing-the-gradle-build-configuration.
        minSdkVersion 23
        targetSdkVersion flutter.targetSdkVersion
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName

        if (isReleaseBuildRequested) {
            ndk {
                abiFilters "armeabi-v7a", "arm64-v8a"
            }
        }
    }

    signingConfigs {
        release {
            if (hasReleaseKeystore) {
                keyAlias keystoreProperties.getProperty('keyAlias')
                keyPassword keystoreProperties.getProperty('keyPassword')
                storeFile rootProject.file(keystoreProperties.getProperty('storeFile'))
                storePassword keystoreProperties.getProperty('storePassword')
            }
        }
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            // Use real release signing if available (CI / maintainers).
            // Fall back to debug signing for local dev convenience.
            signingConfig hasReleaseKeystore ? signingConfigs.release : signingConfigs.debug
        }
    }

}

flutter {
    source '../..'
}

dependencies {
    implementation "androidx.multidex:multidex:2.0.1"
    implementation "androidx.exifinterface:exifinterface:1.3.7"
    implementation "com.google.mlkit:text-recognition:16.0.1"
    implementation "com.google.mlkit:text-recognition-chinese:16.0.1"
    implementation "com.google.mlkit:text-recognition-japanese:16.0.1"
    implementation "com.google.mlkit:text-recognition-korean:16.0.1"
}
